
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'com.jfrog.bintray' version "1.8.4"
    id 'maven-publish'
}

ext.kotlinVersion = "1.4.20"
ext.openrndrVersion = "0.3.45-rc.5"
ext.orxVersion = "0.3.55-rc.5"
//ext.orxVersion = "0.4.0-SNAPSHOT"

repositories {
	mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/openrndr/openrndr/' }
    maven { url 'https://jitpack.io' }
}

group = 'org.ygl'
version = '0.0.17'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.ygl'
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven-libs'
        name = 'openrndr-utils'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/ylegall/openrndr-utils'
        version {
            name = "${project.version}"
            desc = ''
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

def jvmTargetVersion = "11"

compileKotlin {
    kotlinOptions {
        jvmTarget = jvmTargetVersion
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = jvmTargetVersion
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.openrndrOS = "windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.openrndrOS = "linux-x64"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.openrndrOS = "macos"
        break
}

test {
    useJUnitPlatform()
}

dependencies {
    runtimeOnly "org.openrndr:openrndr-gl3:$openrndrVersion"
    runtimeOnly "org.openrndr:openrndr-gl3-natives-$openrndrOS:$openrndrVersion"

    implementation "org.openrndr:openrndr-core:$openrndrVersion"
    implementation "org.openrndr:openrndr-svg:$openrndrVersion"
    implementation "org.openrndr:openrndr-animatable:$openrndrVersion"
    implementation "org.openrndr:openrndr-extensions:$openrndrVersion"
    implementation "org.openrndr:openrndr-filter:$openrndrVersion"
    implementation "org.openrndr:openrndr-ffmpeg:$openrndrVersion"
    implementation "org.openrndr.extra:orx-mesh-generators:$orxVersion"
    implementation "org.openrndr.extra:orx-gui:$orxVersion"
    implementation "org.openrndr.extra:orx-camera:$orxVersion"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testImplementation 'org.assertj:assertj-core:3.15.0'
}


plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.jfrog.bintray' version "1.8.4"
    id 'maven-publish'
}

ext.openrndrVersion = "0.3.37"

repositories {
	mavenCentral()
    jcenter()
    maven { url="https://dl.bintray.com/openrndr/openrndr/" }
    maven { url 'https://jitpack.io' }
}

group = 'org.ygl'
version = '0.0.3'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.ygl'
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven-libs'
        name = 'openrndr-utils'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/ylegall/openrndr-utils'
        version {
            name = "${project.version}"
            desc = ''
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.openrndrOS = "windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.openrndrOS = "linux-x64"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.openrndrOS = "macos"
        break
}

dependencies {
    runtimeOnly ("org.openrndr:openrndr-gl3:$openrndrVersion") { force = true }
    runtimeOnly ("org.openrndr:openrndr-gl3-natives-$openrndrOS:$openrndrVersion") { force = true }

    implementation ("org.openrndr:openrndr-core:$openrndrVersion") { force = true }
    implementation ("org.openrndr:openrndr-svg:$openrndrVersion") { force = true }
    implementation ("org.openrndr:openrndr-animatable:$openrndrVersion") { force = true }
    implementation ("org.openrndr:openrndr-extensions:$openrndrVersion") { force = true }
    implementation ("org.openrndr:openrndr-filter:$openrndrVersion") { force = true }
    implementation ("org.openrndr:openrndr-ffmpeg:$openrndrVersion") { force = true }

    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.61'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

    // replace the following with functional slf4j library when using slf4j
    implementation 'org.slf4j:slf4j-nop:1.7.25'
}
